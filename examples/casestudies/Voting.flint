contract Ballot (Proposing, Voting) {
  // The administrator of the ballot.
  visible let chairperson: Address

  // The accounts which have voted in this ballot.
  var voters: [Address: Voter] = [:]
  visible var votersKeys: [Address] = [] // this can be removed once keys property of dictionary is added to stdlib

  // The list of proposals.
  var proposals: [Proposal] = []
}

Ballot@(any) :: caller <- (any) {
  public init() {
    chairperson = caller

    // Chairperson is a normal voter.
    voters[chairperson] = Voter(1)
    votersKeys[0] = chairperson

    become Proposing
  }
}

Ballot@(Proposing) :: (chairperson) {
  // Add a proposal to the list of proposals to vote on.
  public mutating func addProposal(proposalName: String) {
    proposals[proposals.size] = Proposal(proposalName)
  }

  // Allow an address to vote.
  public mutating func giveRightToVote(voter: Address) {
    // Ensure the voter has not been initialised yet, by checking the weight.
    assert(voters[voter].votingWeight == 0)

    // Create voter and add to dictionary.
    voters[voter] = Voter(1)
    votersKeys[votersKeys.size] = voter
  }

  // Begin the voting phase.
  public mutating func beginVote() {
    become Voting
  }
}

Ballot@(Voting) :: voter <- (votersKeys) {
  // Delegate vote to another voter.
  public mutating func delegate(target: Address) {
    // Ensure the delegator has not already voted, the delegator is not the delegate,
    // and that the delegate has the right to vote.
    assert((voters[voter].hasVoted == false) && (voter != target) && (voters[target].votingWeight != 0))

    // Delegating to a delegate that itself delegates is not allowed, as loops could be formed and
    // unbounded gas consumption is possible. This must be checked explicitly as otherwise votes
    // would be sent to an incorrect proposal.
    assert(voters[target].delegate == 0x0000000000000000000000000000000000000000)

    voters[voter].hasVoted = true
    voters[voter].delegate = target

    // The voting weight of the caller.
    let voterWeight: Int = voters[voter].votingWeight

    // Increase the weight of the delegate.
    voters[target].votingWeight += voterWeight

    if voters[target].hasVoted {
      // If the delegate has already voted for a proposal, increase its number of votes.

      // The proposal the delegate has voted for.
      var votedProposalID: Int = voters[target].votedProposalID // TODO: this should be let, but is var due to a bug.
      proposals[votedProposalID].numVotes += voterWeight
    }
  }

  public mutating func vote(proposalID: Int) {
    if voters[voter].hasVoted { fatalError() }
    voters[voter].hasVoted = true
    voters[voter].votedProposalID = proposalID

    let weight: Int = voters[voter].votingWeight
    proposals[proposalID].numVotes += weight
  }
}

Ballot@(Voting) :: (any) {
  public func getWinningProposalID() -> Int {
    var winningProposalID: Int = 0

    var i: Int = 0
    for let proposal: Proposal in proposals {
      if proposal.numVotes > proposals[winningProposalID].numVotes {
        winningProposalID = i
      }

      i += 1
    }

    return winningProposalID
  }

  public func getWinningProposalName() -> String {
    return proposals[getWinningProposalID()].name
  }
}

struct Voter {
  // The weight of this voter's vote. Increases when other voters delegate
  // their vote to this voter.
  var votingWeight: Int

  // Whether the voter has voted for a proposal.
  var hasVoted: Bool = false

  // The voter this voter has delegated its vote to.
  var delegate: Address = 0x0000000000000000000000000000000000000000

  // The ID of the proposal this voter has voted for.
  var votedProposalID: Int = 0

  public init(weight: Int) {
    votingWeight = weight
  }
}

struct Proposal {
  // The name of the proposal.
  var name: String

  // The number of votes for this proposal.
  var numVotes: Int = 0

  public init(name: String) {
    self.name = name
  }
}
